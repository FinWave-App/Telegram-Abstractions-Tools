plugins {
    id('java')
    id("maven-publish")
}

group 'app.finwave.tat'
version '3.0.3'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.github.pengrad:java-telegram-bot-api:7.4.0'
    implementation 'com.google.code.gson:gson:2.11.0'

    testImplementation platform("org.junit:junit-bom:5.9.1")
    testImplementation "org.junit.jupiter:junit-jupiter"
}

tasks.test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'telegram-abstractions-tools'
            version = project.version

            pom {
                name = 'TelegramAbstractionsTools'
                description = 'Java library for Telegram bots development'
                url = 'https://github.com/FinWave-App/Telegram-Abstractions-Tools'

                licenses {
                    license {
                        name = 'MIT license'
                        url = 'https://raw.githubusercontent.com/FinWave-App/Telegram-Abstractions-Tools/main/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'knst'
                        name = 'isKONSTANTIN'
                        email = 'me@knst.su'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/FinWave-App/Telegram-Abstractions-Tools.git'
                    developerConnection = 'scm:git:ssh://github.com:FinWave-App/Telegram-Abstractions-Tools.git'
                    url = 'https://github.com/FinWave-App/Telegram-Abstractions-Tools'
                }
            }
        }
    }
    repositories {
        maven {
            name = "FinWaveNexus"
            url = "https://nexus.finwave.app/repository/maven-public"
            credentials {
                username = System.getenv("NEXUS_DEPLOY_USERNAME")
                password = System.getenv("NEXUS_DEPLOY_PASSWORD")
            }
        }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/finwave-app/telegram-abstractions-tools"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}